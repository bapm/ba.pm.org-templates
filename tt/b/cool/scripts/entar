#! /usr/local/bin/perl -w

use IO::Prompt;
use Getopt::Euclid;
use Carp;

my $new_tar_file = $ARGV{'<outfile>'};
my @contents     = @{$ARGV{'<infiles>'}};

my $bundledir;

# Auto-bundle multiple files so they unpack cleanly...
if (@contents > 1) {

    # Directory name is tar file name minus suffixes...
    $new_tar_file
        =~ m{ (.*?) (?:[.]tar)? (?:[.]gz)? (?:[.]uu)? $ | (.*?) [.]?}xms;
    $bundledir = $+;

    # Is the directory already there?
    if (-e $bundledir) {
        prompt(-y1, "Bundle directory '$bundledir' already exists. Overwrite? ")
            or exit;
        system "rm -rf $bundledir/*";
        @contents = grep { $_ ne $bundledir } @contents;
    }
    else {
        mkdir($bundledir, 0777);
    }

    # Then populate it and just tar the directory...
    system "cp -r @contents $bundledir";
    @contents = ( $bundledir );
    print "Bundling files in directory '$bundledir'...\n";
}

# Are we already done?
if (-e $new_tar_file) {
    prompt(-y1, "$new_tar_file already exists. Overwrite? ")
        or exit;
}

# Check for optional encodings...
my $uu  = $new_tar_file =~ s{.uu$}{};
my $zip = $new_tar_file =~ s{.gz$}{};

# Tar contents first...
print "tar'ing [@contents] to produce $new_tar_file...\n";
system "tar cf $new_tar_file @contents";

# Gzip if requested...
print "gzip'ing $new_tar_file to produce $new_tar_file.gz...\n" if $zip;
system "gzip $new_tar_file" if $zip;

# UUencode if requested...
$new_tar_file .= ".gz" if $zip;
print "uuencoding $new_tar_file to produce $new_tar_file.uu...\n" if $uu;
system "uuencode $new_tar_file $new_tar_file > $new_tar_file.uu" if $uu;

# Clean up...
unlink $new_tar_file if $uu;
if (-e $bundledir) {
    system "/bin/rm -rf $bundledir";
}


__END__

=head1 NAME

entar - Bundle up a set of files into a tar file and optionally compress it

=head1 VERSION

This documentation refers to entar version 0.0.1

=head1 USAGE

    entar <outfile> <infiles>...

=head1 REQUIRED ARGUMENTS

=over

=item <outfile>

The name of the tar file you wish to be created. It must end in C<.tar>,
C<.tar.gz>, or C<.tar.gz.uu>

=item <infiles>...

The names of one or more files to be incorporated in the tar file.

=back

=head1 DIAGNOSTICS

None

=head1 CONFIGURATION AND ENVIRONMENT

Requires no configuration files or environment variables.


=head1 DEPENDENCIES

Requires the C<Getopt::Euclid> and C<IO::Prompt> modules.

Requires system utilities: C<rm>, C<tar>, C<gzip>, and C<uuencode>.


=head1 BUGS

None reported.
Bug reports and other feedback are most welcome.


=head1 AUTHOR

Damian Conway C<< DCONWAY@cpan.org >>


=head1 COPYRIGHT

Copyright (c) 2007, Damian Conway C<< <DCONWAY@cpan.org> >>. All rights reserved.

This module is free software. It may be used, redistributed
and/or modified under the terms of the Perl Artistic License
(see http://www.perl.com/perl/misc/Artistic.html)


=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.


